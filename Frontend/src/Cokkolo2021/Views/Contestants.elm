module Cokkolo2021.Views.Contestants exposing (..)

import Html exposing (Html, text, h1, div, br)
import Html.Events exposing (onClick)
import Bootstrap.Grid as Grid
import Bootstrap.Button as Button
import Bootstrap.Form.Input as Input
import Bootstrap.Utilities.Spacing as Spacing
import Bootstrap.Table as Table
import List.Extra as List

import Json.Decode as Decode exposing (Decoder)
import Json.Encode as Encode exposing (Value)

import Cokkolo2021.Types.Fight as Fight
import Cokkolo2021.Common exposing (..)
import Settings

type alias ViewState =
  { user : User
  , items : List Contestant
  }

type alias Contestant =
  { username : String
  , eggname : String
  , base : Item
  , waterable : Bool
  , skills : Skills
  , ontoztek : Int
  , ontozott : Int
  }

userToContestant : User -> List Contestant -> Contestant
userToContestant user cs =
  cs
  |> List.find (\c -> c.username == user.username)
  |> Maybe.withDefault
    { username = user.username
    , eggname = user.eggName
    , base = user.base
    , waterable = True
    , skills = user.skills
    , ontoztek = -1
    , ontozott = -1
    }

decode : Decoder Contestant
decode =
  Decode.map7
    Contestant
    (Decode.field "username" Decode.string)
    (Decode.field "eggname" Decode.string)
    (Decode.field "base" itemDecoder)
    (Decode.field "waterable" Decode.bool)
    (Decode.field "skills" skillsDecoder)
    (Decode.field "ontoztek" Decode.int)
    (Decode.field "ontozott" Decode.int)

init : User -> ViewState
init u =
  { user = u
  , items = []
  }

encodeWaterBody : String -> User -> Value
encodeWaterBody target user =
  Encode.object
    [ ("username", Encode.string user.username)
    , ("target", Encode.string target)
    , ("password", Encode.string user.password)
    ]

type Message
  = SwitchToDashboardView
  | SwitchToEggView Contestant
  | PopulateList (List Contestant)
  | WaterUser String
  | WateringSuccess
  | FightRequest User Contestant
  | FightRequestFailure String
  | FightRequestSuccess Contestant (List Fight.FightLog)

view : ViewState -> Html Message
view state =
  [ [ h1 [] [text "RÃ©sztvevÅ‘k"]
      , description
      , Button.button
      [ Button.outlineSecondary
      , Button.attrs [ Spacing.m2 ]
      , Button.onClick SwitchToDashboardView
      ] [text "Vissza"]
      , Table.table
      { options = [ Table.hover ]
      , thead =
          Table.simpleThead
              [ Table.th [] []
              , Table.th [] []
              , Table.th [] [text "TojÃ¡s"]
              , Table.th [] [text "Gazda"]
              , Table.th [] []
              , Table.th [] []
              ]
      , tbody =
        state.items
        |> List.filterNot (\contestant -> contestant.username == state.user.username)
        |> (\list -> userToContestant state.user state.items :: list)
        |> List.indexedMap (\index contestant ->
            [ Table.td [Table.cellAttr (onClick <| SwitchToEggView contestant)] [ text <| String.fromInt (index + 1) ]
            , Table.td [Table.cellAttr (onClick <| SwitchToEggView contestant)] [ displayImage contestant.base.image 61 82 ]
            , Table.td [Table.cellAttr (onClick <| SwitchToEggView contestant)] [ text contestant.eggname ]
            , Table.td [Table.cellAttr (onClick <| SwitchToEggView contestant)] [ text contestant.username ]
            , Table.td [] [ if contestant.username == state.user.username
                            then text "(te vagy)"
                            else if contestant.waterable
                            then Button.button
                              [ Button.outlineSecondary
                              , Button.onClick (WaterUser contestant.username)
                              ] [text "ðŸ’¦"]
                            else text "(ma mÃ¡r megÃ¶ntÃ¶zted)"
                        ]
            , Table.td []
              [Button.button
                [ Button.outlineSecondary
                , Button.onClick (FightRequest state.user contestant)
                ] [text "ðŸ¤œðŸ¤›"]]
            ] |> Table.tr (if not contestant.waterable then [Table.rowSuccess] else [])
        )
        |> Table.tbody []
      }
      ] |> Grid.col []
  ] |> Grid.row []

description : Html Message
description =
  [ text "Itt van az Ã¶sszes versenyzÅ‘, egyszer egy nap megÃ¶ntÃ¶zheted Å‘ket (ami neked nem kerÃ¼l semmibe) Ã©s Å‘k kapnak egy kÃ¶lnit. Persze ha mÃ¡sok tÃ©ged megÃ¶ntÃ¶znek akkor te kapsz egy kÃ¶lnit."
  , br [] []
  , br [] []
  , text "RÃ¡klikkelhetsz egy tojÃ¡sra, hogy lÃ¡ssad milyen felszerelÃ©se Ã©s milyen tulajdonsÃ¡gai vannak annak a tojÃ¡snak. Azt is lÃ¡tod, hogy hÃ¡nyan Ã¶ntÃ¶ztÃ©k meg azt a gazdit, Ã©s hÃ¡nyat Ã¶ntÃ¶zÃ¶tt meg az a gazdi."
  , br [] []
  , br [] []
  , text "KÃ¶lcsÃ¶n kenyÃ©r vissza jÃ¡r, tehÃ¡t azt javasolom, hogy ne spÃ³rolj az Ã¶ntÃ¶zÃ©ssel :)"
  , br [] []
  ] |> div []
